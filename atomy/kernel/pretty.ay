namespace(atomy/pretty)

dynamic(multiline?, false)
dynamic(context, #top)
dynamic(colored?, false)

comma-delim(ds) :=
  hsep(comma punctuate(ds))

ident(n) := text(n [0, 1] + n [1 .. -1] tr("_", "-") gsub("_ns_", "/"))

module(Atomy::AST):
  Block unamb := pretty
  ClassVariable unamb := pretty
  Constant unamb := pretty
  GlobalVariable unamb := pretty
  (InlinedBody ? @expressions size < 2) unamb := pretty
  (Tree ? @nodes size < 2) unamb := pretty
  InstanceVariable unamb := pretty
  List unamb := pretty
  Particle unamb := pretty
  Primitive unamb := pretty
  QuasiQuote unamb := pretty
  Quote unamb := pretty
  ScopedConstant unamb := pretty
  Send unamb :=
    if(from?(#binary, #send))
      then: pretty
      else: Doc parens(pretty)
  Splice unamb := pretty
  String unamb := pretty
  ToplevelConstant unamb := pretty
  Unary unamb := pretty
  Unquote unamb := pretty
  Variable unamb := pretty
  Node unamb := Doc parens(pretty)
  _ unamb := pretty

from(*where, &y) :=
  let(context = where flatten, &y)

from?(*where) :=
  [^context, where] match:
    [(a: Array), (b: Array)] ->
      a any? [x]: b include?(x)
    [(a: Array), b] ->
      a include?(b)
    [a, (b: Array)] ->
      b include?(a)
    [a, b] ->
      a == b

export-to(atomy):
  String pretty := super colored(#yellow)
  Integer pretty := super colored(#blue)
  Float pretty := super colored(#blue)
  TrueClass pretty := super colored(#green)
  FalseClass pretty := super colored(#red)
  NilClass pretty := super colored(#black)
  Object pretty := Doc text(inspect)

  Object show := pretty render

  Doc pretty := self

  Array pretty :=
    doc: brackets(comma-delim(self all-pretty))

  Particle pretty := doc:
    wildcard = [c]:
      if(c equal?(_undefined))
        then: '_
        else: c

    operator? = self message to-s !~ r"^[a-z_]"

    args = self arguments collect(&#(wildcard [_]))

    msg+args =
      if(operator?)
        then:
          text(self message to-s) <+> args first unamb
        else:
          ident(self message to-s) <>
            parens(comma-delim(args all-pretty))

    partial =
      if(self receiver equal?(_undefined))
        then:
          if(operator?)
            then: parens(msg+args)
            else: msg+args
        else: parens(self receiver unamb <+> msg+args)

    text("#") <> partial

  Particle to-s := show

  Symbol pretty := do:
    s = to-s
    if(s =~ r"^[\p{Ll}_]([\p{L}\d\p{S}!@#%&*\-_\\:.\/\?])*$"(u))
      then: Doc text("#") <> Doc ident(s)
      else: Doc text("#") <> s pretty

  Hash pretty := doc:
    text("#") <>
      brackets $:
        comma-delim $:
          self to-a collect [[k, v]]:
            k unamb <+> text("->") <+> v unamb

  module(Atomy::AST):
    Assign pretty :=
      from(#assign):
        doc: @lhs unamb <+> text("=") <+> @rhs unamb

    BinarySend pretty :=
      from(#binary):
        doc: @lhs unamb <+> text(@operator) <+> @rhs unamb

    Block pretty :=
      doc:
        args =
          if(@arguments empty?)
            then: empty
            else: brackets(from(#list): comma-delim(@arguments all-pretty))

        contents = from(#list): comma-delim(@contents all-pretty)
        if(from?(#send-block) && from?(#top))
          then: args <> colon <+> contents
          else: args <+> lbrace <+> contents <+> rbrace

    BlockPass pretty :=
      from(#unary):
        doc: text("&") <> @body unamb

    Class pretty := doc:
      sub =
        if(@superclass kind-of?(Primitive) && @superclass value == #nil)
          then: empty
          else: text("<") <+> from(#binary): @superclass unamb

      name =
        @name match:
          Rubinius::AST::ClassName -> text(@name) name to-s
          _ -> @name unamb

      body = lbrace <+> semi-delim(@body) expressions <+> rbrace

      text("class") <> text("(") <> name <+> sub <> text(")") <+> body

    ClassVariable pretty := doc: ident(name to-s)

    Constant pretty := doc: text(@identifier)

    Define pretty :=
      from(#define):
        doc: @lhs unamb <+> text(":=") <+> @body unamb

    GlobalVariable pretty := doc: ident(name to-s)

    InstanceVariable pretty := doc: ident(name to-s)

    List pretty :=
      from(#list):
        doc: brackets(comma-delim(@elements all-pretty))

    Macro pretty := doc:
      text("macro") <+> parens(@pattern pretty) <+> @body pretty

    Particle pretty := doc: text("#") <> ident(@name)

    Primitive pretty := doc:
      @value match:
        -- TODO: glitch with #doc/#onto
        #"self" -> text("self")
        #true -> text("true")
        #false -> text("false")
        #nil -> text("nil")
        x -> text(x inspect)

    QuasiQuote pretty :=
      from(#unary):
        doc: text("`") <> @expression unamb

    Quote pretty :=
      from(#unary):
        doc: text("'") <> @expression unamb

    ScopedConstant pretty :=
      from(#scoped-constant):
        doc:
          @parent unamb <> text("::") <> text(@identifier)

    Send pretty :=
      doc:
        block =
          from(#send-block, ^context):
            if(@block) then: @block unamb; else: empty

        with-block = [x]:
          condition:
            ^multiline? && @block is-a?(Block) -> do:
              cs = from(#top): vcat(@block contents all-pretty) nest(2)
              if(from?(#top))
                then: x <> colon // cs
                else: x <+> lbrace // cs // rbrace

            from?(#top) && @block is-a?(Block) && @block arguments empty? ->
              x <> block

            otherwise ->
              x <+> block

        args =
          from(#list):
            if(@arguments empty?)
              then: empty
              else: parens(comma-delim(@arguments all-pretty))

        condition:
          @receiver kind-of?(Primitive) && @receiver value == #"self" ->
            with-block [ident(@method-name) <> args]

          otherwise ->
            with-block [
              from(#send) { @receiver unamb } <+>
                ident(@method-name) <> args
            ]

    Splice pretty :=
      from(#unary):
        doc: text("~*") <> @expression unamb

    String pretty := Doc text(@value inspect)

    ToplevelConstant pretty := doc: text("::") <> text(@identifier)

    Unary pretty :=
      from(#unary):
        doc: text(@operator) <> @receiver unamb

    Unquote pretty :=
      from(#unary):
        doc: text("~") <> @expression unamb

    Variable pretty := doc: ident(@name)

    InlinedBody pretty := doc:
      if(^multiline?)
        then:
          from(#top): vcat(@expressions all-pretty)
        else:
          from(#list): comma-delim(@expressions all-pretty)

    Or pretty := `(~@a || ~@b) pretty

    And pretty := `(~@a && ~@b) pretty

    Ensure pretty := doc:
      @body pretty <+> text("ensuring:") <+> @ensure pretty

    Tree pretty := doc:
      if(^multiline?)
        then:
          from(#top): vcat(@nodes all-pretty)
        else:
          from(#list): comma-delim(@nodes all-pretty)

Array all-pretty := collect [x]: x pretty

Doc colored(color) := do:
  -- only makes sense to colorize if we're outputting to a terminal
  unless(^output-port tty? && ^colored?):
    return(self)

  codes =
    [ #black
      #red
      #green
      #yellow
      #blue
      #magenta
      #cyan
      #white
    ] zip((0 .. 7) to-a)

  hash = Hash [codes]

  Doc text("\e[9" + hash [color] to-s + "m") <> self <> Doc text("\e[0m")